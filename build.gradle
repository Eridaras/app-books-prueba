plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.6.1"
    id "org.flywaydb.flyway" version "9.8.1"
}

group 'com.distribuida.apps.books'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    helidonVersion = '3.1.0'
    mainClass='io.helidon.microprofile.cdi.Main'
}

dependencies {
    implementation platform("io.helidon:helidon-dependencies:${project.helidonVersion}")
    implementation 'io.helidon.microprofile.server:helidon-microprofile-server'
    implementation 'io.helidon.microprofile.rest-client:helidon-microprofile-rest-client'
    implementation 'org.glassfish.jersey.media:jersey-media-json-binding'
    implementation 'io.helidon.microprofile.tracing:helidon-microprofile-tracing'
    implementation 'io.helidon.tracing:helidon-tracing-jaeger'

    //postgres
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.1'

    //hikari
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'

    //JPA
    implementation 'jakarta.annotation:jakarta.annotation-api'
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api'
    implementation 'jakarta.inject:jakarta.inject-api'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api'
    implementation 'jakarta.json.bind:jakarta.json.bind-api'
    implementation 'jakarta.persistence:jakarta.persistence-api'
    implementation 'jakarta.transaction:jakarta.transaction-api'
    implementation 'io.helidon.integrations.cdi:helidon-integrations-cdi-jta-weld'
    implementation 'io.helidon.integrations.cdi:helidon-integrations-cdi-hibernate'
    implementation 'io.helidon.integrations.cdi:helidon-integrations-cdi-jpa'
    implementation 'org.hibernate.validator:hibernate-validator-cdi'
    implementation 'org.glassfish:jakarta.el'

    //OPENAPI
    implementation 'io.helidon.microprofile.openapi:helidon-microprofile-openapi'
    //implementation 'io.helidon.integrations.openapi-ui:helidon-integrations-openapi-ui'

    //flyway
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.11.0'
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/distribuida'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
}

sourceSets {
    main {
        output.resourcesDir = file("${buildDir}/classes/java/main")
    }
}

task copyLibs(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath
    into 'build/libs/libs'
}

jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes ('Main-Class': "${project.mainClass}" ,
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}

application {
    mainClass = "${project.mainClass}"
}